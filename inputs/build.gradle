buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

repositories {
    jcenter()
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 23
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.github.yoojia:next-inputs:1.3.3'
}

ext{
    projVersion = '1.2.4'
    projName = 'NextInputs-Android'
    projDesc = 'NextInputs-Android: Android binding for NextInputs'
    projURL = "https://github.com/yoojia/NextInputs-Android"
    projVCS = "https://github.com/yoojia/NextInputs-Android.git"
}

group 'com.github.yoojia'
version projVersion

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        pom.project {
            name projName
            description projDesc
            url projURL
            inceptionYear '2016'

            packaging 'aar'
            groupId 'com.github.yoojia'
            artifactId 'next-inputs-android'
            version projVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection projVCS
                url projURL

            }
            developers {
                developer {
                    id 'yoojia'
                    name 'Yoojia Chen'
                    email 'yoojiachen@gmail.com'
                    url 'https://yoojia.github.io'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = projName
        userOrg = 'yoojia'
        licenses = ['Apache-2.0']
        vcsUrl = projVCS
        version {
            name = projVersion
            desc = projDesc
            vcsTag = projVersion
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']

            gpg {
                sign = true
            }

            mavenCentralSync {
                sync = true
                user = project.hasProperty('SONATYPE_USER') ? project.property('SONATYPE_USER') : ""
                password = project.hasProperty('SONATYPE_PASS') ? project.property('SONATYPE_PASS') : ""
                close = '1'
            }
        }
    }
}